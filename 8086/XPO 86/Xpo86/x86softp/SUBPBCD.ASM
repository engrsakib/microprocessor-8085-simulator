        PAGE 70

  TITLE -- PACKED BCD 16 DIGIT SUBTRACTION.

;THIS PROG.SUBTRACTS TWO 16 DIGIT PKED.BCD NOS.
;FIRST NO.IS AT ADDRESS 000H:[1000H TO 1007H]
;& SECOND NO. IS AT 0000H:[1010 H TO 1017 H]
;THE RESULT WILL BE STOREDB TO DESTINATION
;ADDR 0000H:[1050 H TO 1058 H].
; [1007 H]....[1001 H] [1000 H] FIRST NO.
;-
; [1017 H]....[1011 H] [1010 H] SECOND NO.
;--------------------------------------------
; [1057 H]....[1051 H] [1050 H]  RESULT
; [1058 H] BORROW
;
;REG.USED/DESTROYED :- AX,CX,DX,SI,DI ETC.
;MEM. LOCATIONS MODIFIED :- 1050 H TO 1058 H.
;
;COMMON SCRATCH PAD AREA ALLOTED :-
;SEG.=0000H, ADDR.=1000H ONWARDS.
;ie 0000:1000 H ONWARDS.
; 
; INPUT TO THE PROG. :-
;  SEG 0000H
;  ADDR        DATA
; [1000 H] -- LOWEST SIGNIFICANT
;             2 DIGITS OF FIRST NO.
; [1001 H] -- NEXT SIGNIFICANT
;             2 DIGITS OF FIRST NO.
;   |          |
; [1007 H] -- MOST SIGNIFICANT
;             2 DIGITS OF FIRST NO.
;
; [1010 H] -- LOWEST SIGNIFICANT
;             2 DIGITS OF SECOND NO.
; [1011 H] -- NEXT SIGNIFICANT
;             2 DIGITS OF SECIND NO.
;   |          |
; [1017 H] -- MOST SIGNIFICANT
;             2 DIGITS OF SECOND NO.
;
; OUTPUT OF THE PROG. :-
;   SEG 0000H
;   ADDR        DATA
; [1050 H] -- LOWEST SIGNIFICANT
;             2 DIGITS OF RESULT NO.
; [1051 H] -- NEXT SIGNIFICANT
;             2 DIGITS OF RESULT NO.
;   |          |
; [1057 H] -- MOST SIGNIFICANT
;             2 DIGITS OF RESULT NO.
; [1058 H] -- IF BORROW THEN 01H ELSE 00H

        SUBPBCD  SEGMENT
        ASSUME CS:SUBPBCD, DS:SUBPBCD
        ASSUME ES:SUBPBCD
; 'ASSUME' IS AN ASSEMBLER DIRECTIVE. IT HAS
; NOTHING TO DO WITH 8086. IT IS ONLY USED
; TO TELL THE ASSEMBLER (MASM), WHERE THE
; CODE, DATA, STACK ETC. IS LOCATED.
; IT IS ONLY A FORMALITY FOR MASM.

SCPD     EQU 0000H ; SCRATCH PAD SEGMENT.
ADR_1NO  EQU 1000H ; ADDRESS OF 1st NO.
ADR_2NO  EQU 1010H ; ADDRESS OF 2nd NO.
ADR_RSLT EQU 1050H ; ADDRESS OF RESULT
BYTENO   EQU 08H   ; NO. OF BYTES TO BE SUB'ED

ORG 100H       ;COM FORMAT IS RECOMMENDED.

STRT:
JMP SKIP_DATA  ;SKIP LOOK UP/DATA BASES IF ANY

;ON KIT YOU NEED TO DO INIT FOR SP TO SEPARATE
;STACK OF YOUR PROGRAME FROM MONITORS, DS AS
;SHOWN BELOW.SS,ES ARE SET TO 0000 AT POWER ON
;BY MONITOR. YOU CAN OFCOURSE CHANGE IT BY
;ADDING A FEW MORE MOVES. BUT WHILE WORKING ON
;PC YOU CAN`T TOUCH SEGMENT REGISTERS SINCE
;THEY ARE SET BY DOS LOADER,SO MANAGE THESE
;INSTRUCTIONS AS SHOWN IN COMMENT FIELD.

SKIP_DATA:
MOV AX,10FFH    ; INIT OF SP FOR KIT
MOV  SP,AX      ; <-- MOV AX,AX  ON PC
PUSH CS         ; INIT FOR DS
POP DS          ;
NOP             ; TO LOAD KIT INTERRUPTS
NOP             ; (A0-BF) IN PC
NOP             ; REPLACE THE 3 NOPS
; CALL LDINPC   ; WITH THIS CALL.
MOV AX,SCPD     ; TO SPECIFY SCPD LOCn ON KIT.
NOP             ; <-- MOV AX,CS  ON PC
MOV ES,AX       ;
MOV SI,ADR_1NO  ; ADDR. OF 1st NO. IN SI.
MOV DX,ADR_2NO  ; ADDR. OF 2nd NO. IN DX.
MOV DI,ADR_RSLT ; ADDR. OF RESULT IN DI.
MOV CX,BYTENO   ; NO. OF BYTES IN CX.
CLC             ; CLEAR CARRY
LUP1:           ;
XCHG DX,DI      ; DI=1010H, DX=1050H.
MOV AL,ES:[SI]  ; TAKE FIRST DATA BYTE.
SBB AL,ES:[DI]  ; SUBTRACT SECOND NO. BYTE
DAS             ; DECIMAL ADJUST AFTER SUBn.
XCHG DX,DI      ; DI=1050H, DX=1010H
MOV ES:[DI],AL  ; STORE RESULT TO DESTINATION
INC DI          ;
INC SI          ;
INC DX          ;
LOOP LUP1       ; REPEAT FOR NO. OF BYTES.
MOV AL,00H      ;
MOV ES:[DI],AL  ;
ADC ES:[DI],AL  ; BORROW = [1058 H]
INT 0A3H        ; GOTO COMMAND = MODE ON KIT.
                ; ON PC RETURN TO DOS.
                ; IF YOU ARE RUNNING YOUR
                ; SOFTWARE ON PC INSERT HERE
                ; INCLUDE KIT_INTS.ASM
                ; *******************
                                
SUBPBCD ENDS     ; READ FOR MORE INFO TITLE OF
        END STRT ; KIT_INTS.ASM

