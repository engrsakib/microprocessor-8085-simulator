
PAGE,85
;************************************************
TITLE  DC MOTOR SPEED CONTROLLER (ADC+RELAY CARD)
;************************************************


DCMTRC  SEGMENT

ASSUME CS:DCMTRC, DS:DCMTRC ,ES:DCMTRC

ORG 100H  ;COM FORMAT IS RECOMMENDED.

STRT: JMP SKIP_DATA  ;SKIP DATA BASES IF ANY

;ON KIT  YOU  NEED  TO  DO  INIT  FOR SP
;[TO SEPARATE STACK OF YOUR PROGRAME FROM
;MONITORS],DS AS SHOWN BELOW.SS,ES ARE SET
;TO  0000H  AT  POWER  ON  BY  MONITOR.
;YOU  CAN  OFCOURSE  CHANGE  IT BY ADDING
;A FEW MORE MOVE`S.BUT WHILE WORKING ON PC
;YOU CAN NOT TOUCH SEGMENT REGISTERS SINCE
;THEY ARE SET BY DOS LOADER,SO MANAGE THESE
;INSTRUCTIONS  AS  SHOWN  IN  COMMENT FIELD.

MSG1:  DB "START?",03H

MSG2:  DB "SPEED?",03H

MSG3:  DB "DIR REV?",03H

SKIP_DATA:
MOV AX,11FFH  ;INIT OF SP FOR KIT
MOV SP,AX     ;MOV AX,AX   ON PC
PUSH CS       ;THIS IS BEST WAY TO LOAD DS
POP DS        ;WITH SEGMENT VALUE.
MOV AX,0000H  ;SCPD SEG.
MOV ES,AX
NOP           ;LOAD KIT INT S (A0-BF) IN PC
NOP           ;CALL LDINPC
NOP           ;PLACE ABOVE CALL IN NOP`S
INT 0ACH      ;CLEAR DISPLAY

MOV BX,OFFSET MSG1 ;DISPLAY "START ?"
INT 0AFH           ;MESSAGE OUT

TEST:
MOV AH,08H    ;READ
INT 0A1H      ;LOOK FOR KEYBOARD I/P
CMP AL,0DH    ;CMP KEY PRESS IS ENTER (CR)
JZ START
JMP TEST

;8255 INITIALISATION & ADC.

START:
MOV AX,8080H ;INIT 8255U- A,B,C-O/P.
MOV DX,8006H
OUT DX,AX
MOV AX,0101H ;PC0=1,SMALLEST COUNT.
MOV DX,8004H
OUT DX,AX
MOV DX,8000H
OUT DX,AX

STORE:
MOV ES:[1000H],AX

SPEED:
INT 0ACH     ;CLEAR DISPLAY
MOV BX,OFFSET MSG2 ;DISPLAY "SPEED ?"
MOV AL,02H   ;NO OF DIGIT DISPLAYED
MOV AH,10H   ;FOR HEX NO.SYSTEM
INT 0B9H     ;MESSAGE OUT + RECEIVE NUMBER

MOV AL,DL
MOV AH,DL
MOV DX,8000H ;OUT ENTERED DATA ON PORT A
OUT DX,AX

INT 0ACH     ;CLEAR DISPLAY

MOV BX,OFFSET MSG3 ;DISPLAY "DIR REC ?"
INT 0AFH

MOV AH,08H   ;READ
INT 0A1H     ;LOOK FOR KEYBOARD I/P
CMP AL,0DH   ;COMPARE KEY PRESSED IS ENTER (CR)
JNZ SPEED    ;GOFOR SPEED WITH SAME DIRECTION
MOV AX,2222H ;SLOW DOWN THE MOTOR
MOV DX,8000H ;OUT ENTERED DATA ON PORT A
OUT DX,AX
MOV CX,0FFFH    ;DELAY COUNT
INT 0AAH        ;CALL DELAY
MOV AX,0101H    ;STOP THE MOTOR
MOV DX,8000H    ;OUT ENTERED DATA ON PORT A
OUT DX,AX
MOV DX,ES:[1000H]
MOV BX,0202H
XOR DX,BX
OR DX,0101H
MOV AX,DX
MOV DX,8004H ;DIR GIVEN TO PORT C OF 8255
OUT DX,AX
JMP STORE

;IF YOU ARE RUNNING YOUR SOFTWARE ON PC
;INSERT HERE -->INCLUDE KIT_INTS.ASM
;READ FOR MORE INFO TITLE OF KIT_INTS.ASM

DCMTRC  ENDS
END STRT
                                                                              
