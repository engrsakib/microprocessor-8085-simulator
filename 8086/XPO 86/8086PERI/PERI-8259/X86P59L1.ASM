       PAGE,80
       TITLE--USING 8259 PERIWARE.

;****FOLLOWING LISTING IS W.R.T. BATTERY BACKED RAM AT 1250:0100H BUT THIS
;    PROGRAMME CAN WORK IN SCRATCH PAD RAM LOCATED AT 00000H .PL. ENTER
;    THE SAME FROM 0110:0100 AND EXECUTE.
;    FOLLOW  THE STEPS MUST BE FOLLOWED BEFORE USING PERIWARE 8259:  
;    1) DISABLE INTERNAL 8259 BY SHORTING 1-2 OF LINK L33 USING SHORT
;       LINK IF NOT SHORTED ON MB(KIT).
;    2) TO SELECT INT OF PERIWARE SHORT 1-2 OF L18 ON MB(KIT).
;    3) TO GIVE INTR FOR PERIWARE SHORT 1-2 OF L19 ON MB(KIT).
;    4) TO SELECT INT OF PERIWARE SHORT 2-3 OF L3  ON CONVERTER CARD.   
;****A PROGRAM TO DEMOSTRATE THE USE OF 8259 PIC. HERE ONLY MASTER 8259
;    IS USED. IN THE MAIN PROGRAM "XPO" IS DISPLAYED. IN THE INTERRUPT 
;    SERVICE LOOP, "INT 0" IS DISPLAYED. 

;          DATA BUS  COMMENTS
;
;START	   13        ICW1
;
;STEP1     90	     ICW2
;
;STEP2     01        ICW4
;
;STEP3     FE        ENABLE IR0. 
;
;STEP4  : NOW CPU IS FREE RUNNING INDICATED BY HALF 
;         GLOWING LEDS AND "XPO" IS DISPLAYED WAITING 
;         FOR INTERRUPT. USING PATCH CORD CONNECT IR0 
;         TO VCC. CORD CAN BE REMOVED AFTER INTA ARRIVAL.     
;
;
;         DATA BUS  COMMENTS  
;
;START    FF        FIRST INTA WITN CALL.
;
;STEP5    90        INT 90H FOR IR0. 
;
;STEP6    20        OCW2 - EOI.
;
;STEP11 : FREE RUNNING CPU DISPLAYING "INT 0" WITH SOME
;         DELAY AND AGAIN DISPLAY REVERTS TO "XPO" 
;         AFTER RETURNING TO MAIN LOOP.
;
;
;
;
;
;
X868259  SEGMENT

         ASSUME CS:X868259, DS:X868259 ,ES:X868259
         ORG 100H   ;COM FORMAT IS RECOMMENDED.
STRT:    
JMP SKIP_DATA       ;SKIP LOOK UP/ DATA BASES IF ANY

;ON KIT YOU NEED TO DO INIT FOR SP [TO SEPARATE STACK OF YOUR PROGRAME FROM
;MONITORS],DS AS SHOWN BELOW.SS,ES ARE SET TO 0000 AT POWER ON BY MONITOR.
;YOU CAN OFCOURSE CHANGE IT BY ADDING A FEW MORE MOVE`S.
;BUT WHILE WORKING ON PC YOU CAN`T TOUCH SEGMENT REGISTERS SINCE THEY ARE
;SET BY DOS LOADER,SO MANAGE THESE INSTRUCTIONS AS SHOWN IN COMMENT FIELD.  


                                            
SKIP_DATA:
MOV  AX,11F0H    
MOV  SP,AX  	;INIT OF SP,DS(=CS).ES,SS=0000 
		;ALREADY ON KIT.              
PUSH CS    	;FETCH PRESENT CS IN AX
POP  AX    	;AND LOAD INTO DS SINCE DS=CS 	
MOV  DS,AX   
        
NOP             ;FOR PC
NOP             ;PLACE CALL IN PLACE OF 3 NOP`S
NOP             ;

MOV  CX,AX         ;SEG ADDR OF ISR FOR IR0
MOV  SI,OFFSET ISR ;OFFSET ADDR  
MOV  AL,90H	   ;INIT OF 0000:0240H-44H BYTES SO 
		   ;ON INTERRUPT IR0 "INT 90,91..
		   ;ARE EXECUTED.
INT  0BEH          ;                       
MOV  AL,13H    	   ;EDGE,SINGLE	
		   ;ICW1 A1=0                         
MOV  DX,01A0H      ;                      
OUT  DX,AL         ;                    
MOV  AL,90H	   ;ISR OF IR0 =INT 90H ETC
MOV  DX,01A2H      ;A1=1	
OUT  DX,AL 	   ;ICW2	
MOV  AL,01H	   ;8086,NORMAL EOI,NON BUFERED,
		   ;NON SPECIAL,NEST
OUT  DX,AL	   ;ICW4,A1=1
MOV  AL,0FEH	   ;ENABLE IR0.APPLY L TO H PULSE 
		   
OUT  DX,AL 	   ;OCW1                             
STI	           ;                        
LOOP1:   
MOV  AX,00FFH      ;DELAY COUNT                       
LOOP2:   
MOV  CX,00FFH      ;DELAY COUNT                           
INT  0AAH          ;DELAY
DEC  AX            ;                     
JNZ  LOOP2         ;                      
INT  0ACH          ;CLEAR DISPLAY
MOV  BX,OFFSET MSG1;POINTER TO "XPO"
INT  0AFH          ;OUTMSG UTILITY 
MOV  AH,0BH        ;TEST FOR KEY PRESS
INT  0A1H          ;LOGICAL I/O
CMP  AL,0FFH
JZ   RDESC    
JMP  KEEP	                                   
RDESC:
MOV  AH,08H        ;READ KEY
INT  0A1H          ;LOGICAL I/O
CMP  AL,1BH
JZ   EXIT
KEEP:
STI
JMP  LOOP1
EXIT: 
INT 0A3H           ;CMD MODE.
NOP	                                   
NOP	                                   
NOP	                                   
NOP
NOP
;NOW APPLY L TO H PULSE TO IR0 USING STEP SWITHC. 
;8259 WILL FEED INT 90H FOR IR0 INTERRUPT.
;ISR FOR 8259--IR0.PRESENTLY MAPPED TO INT 90H BY ICW2.
         
ISR:     
INT	0ACH     	;CLEAR TO NEW LINE
MOV	BX,OFFSET MSG2	;POINTER TO MESSAGE  
			;ENDING WITH ETX.
INT	0AFH 		;OUTMSG UTILITY.	
MOV	AL,20H          ;                    
MOV	DX,01A0H        ;                    
OUT	DX,AL           ;OCW2--NON SPECIFIC
IRET	                                   

MSG1: DB "XPO",03H    	;MESSAGES "XPO".
MSG2: DB "INT 0",03H    ;MESSAGE "INT 0".

X868259  ENDS
    END  STRT

